; BIOS code

_global:
                ;    ERROR 100: root directory not found.
 
ER100_STR_LEN:     F[CD]  0x9
ER100_STR:          [CE]  "ERRO"
                    [CF]  "R 10"
                    [D0]  "0: r"
                    [D1]  "oot "
                    [D2]  "dire"
                    [D3]  "ctor"
                    [D4]  "y no"
                    [D5]  "t fo"
                    [D6]  "und."
                    
                ;    ERROR 101: "boot" directory not found on "/".
ER101_STR_LEN:      [D7]  0xC      ; Text word length
ER101_STR:          [D8]  "ERRO"
                    [D9]  "R 10"
                    [DA]  "1: ""
                    [DB]  "boot"
                    [DC]  "" di"
                    [DD]  "rect"
                    [DE]  "ory "
                    [DF]  "not "
                    [E0]  "foun"
                    [E1]  "d on"
                    [E2]  " "/""
                    [E3]  "."
                    
                ;    ERROR 102: "boot.exe" file not found on "/boot".
ER102_STR_LEN:      [E4]  0xC      ; Text word length
ER102_STR:          [E5]  "ERRO"
                    [E6]  "R 10"
                    [E7]  "2: ""
                    [E8]  "boot"
                    [E9]  ".exe"
                    [EA]  "" fi"
                    [EB]  "le n"
                    [EC]  "ot f"
                    [ED]  "ound"
                    [EE]  " on "
                    [EF]  ""/bo"
                    [F0]  "ot"."
                    
                        
ROOT_DIR_EXT:       [F1]  "dir"     ; These 3 to check the root directory
ROOT_NAME_LEN:      [F2]  0x1
ROOT_NAME:          [F3]  "root"
                
BOOT_DIR_DIR_EXT:   [F4]  "dir"     ; boot directory
BOOT_DIR_NAME_LEN:  [F5]  0x1
BOOT_DIR_NAME:      [F6]  "boot"
                
BOOT_EXE_DIR_EXT:   [F7]  "exe"     ; boot executable
BOOT_EXE_NAME_LEN:  [F8]  0x1
BOOT_EXE_NAME:      [F9]  "boot"


                        ; Checks if the Root directory exists
_start:        F[00]  NOP #0x000000
                [01]  NOP #0x000000
                [02]  NOP #0x000000
               F[03]  ACC 0x0000FB          ; Load the root extension
                [04]  CMP ROOT_DIR_EXT      ; Compare with "dir"
                [05]  JNE _rootDirNotFound
                [06]  ACC 0x0000FC          ; root filename length
                [07]  CMP ROOT_NAME_LEN     ; 1
                [08]  JNE _rootDirNotFound
                [09]  ACC 0x0000FD          ; root filename
                [0A]  CMP ROOT_NAME         ; "root"
                [0B]  JNE _rootDirNotFound
                
                        ; Find "boot" dir
                [0C]  PSH .return1              ; Return Address (Call-like) 
                [0D]  PSH #0x0000FF             ; root directory data size Address
                [0E]  PSH BOOT_DIR_DIR_EXT      ; "dir" extension
                [0F]  PSH BOOT_DIR_NAME_LEN     ; 1 = filename length in words
                [10]  PSH &BOOT_DIR_NAME        ; First Address of the string (which is the only Address, as "boot" is 4 chars long (1 word))
                [11]  JMP _searchFile
.return1:       [12]  JZ _bootDirNotFound
                        
                        ; Find "boot" executable
                [13]  PSH .return2              ; Return Address (Call-like)
                [14]  PSH 0x000000              ; Push Accumulator content ("boot" dir first Address)
                [15]  JMP _getFileDataSizeAddress
.return2:       [16]  PSH .return3              ; Return Address (Call-like)
                [17]  PSH 0x000000              ; Address of the boot directory data size, already on Accumulator (given by previous sub-routine called)
                [18]  PSH BOOT_EXE_DIR_EXT      ; "exe" extension
                [19]  PSH BOOT_EXE_NAME_LEN     ; 1 = filename length in words
                [1A]  PSH BOOT_EXE_NAME         ; First Address of the string (which is the only Address, as "boot" is 4 chars long)
                [1B]  JMP _searchFile
.return3:       [1C]  JZ _bootExeNotFound
                [1D]  JMP _foundBootExe


; _getFileDataSizeAddress(fileAddress):
;
;   Given the starting Address of a file, will RET 0x000000urn by the Accumulator the Address of the "Data Size" header of this file.
;
;
_local:
    currentAddress: {}

_getFileDataSizeAddress:                
                [1E]  POP #0x0              ; Pops to the Accumulator
                [1F]  ADD #0x2              ; Now the Address points to the filename length (jumps over parent Address and file extension)
                [20]  STO &currentAddress   ; Load it on Accumulator
                [21]  ACC 0x000000          ; Loads the filename length to Accumulator
                [22]  ADD currentAddress    ; Add the Address by the number of filename words (if filename words == 1, skip by 1)
                [23]  ADD #0x1              ; Skip the Flags
                [24]  RET 0x000000

;
; _searchFile(targetParentDataSizeAddress, desiredExtension, desiredFilenameWords, desiredFilenameAddress)
;
;   Returns by the Accumulator the starting Address of the found file, else RET 0x000000urns 0
;

_local:
    currentFileAddress: {}              ; Current File Address in directory of the searchen file
    maximumFileAddress: {}              ; Maximum File Address in directory of the searchen file

    desiredExtension: {}                ; File extension in search
    desiredFilenameLength: {}           ; Filename length (in words) in search
    desiredFilenameFirstAddress: {}     ; Filename first Address in search

    currentAddressHeader: {}            ; Holds the actual Address of the actual header

    counterFilenameLength: {}   
    currentAddressDesiredFilename: {}
    currentContentDesiredFilename: {}

_searchFile:    [25]  POP &desiredFilenameFirstAddress    ; desiredFilenameFirstAddress;
                [26]  POP &desiredFilenameLength    ; desiredFilenameLength;
                [27]  POP &desiredExtension         ; desiredExtension;
                [28]  POP &currentFileAddress       ; ParentDataSizeAddress, store it on Accumulator
                [29]  ACC currentFileAddress        ; Load it on Accumulator
                [2A]  ACC 0x000000                  ; Get the content of the Address, the root data size, default = 32
                [2B]  ADD currentFileAddress        ; Add the initial Address, to get the maximum Address to look for
                [2C]  STO &maximumFileAddress       ; Store it at (Maximum Address in directory of the searchen file)
                
        ; Checks if the Address is in boundaries
.loopNextFile:  [2D]  INC &currentFileAddress       ; Actual File Address ++
                [2E]  ACC currentFileAddress        ; Move it to Accumulator
                [2F]  CMP maximumFileAddress        ; Compare it with max Address
                [30]  JG .fileNotFound              ; If current Address > max Address
                
                        ; Checks if the Address of the file in the directory is 0 (end of file of a directory)
                [31]  ACC 0x000000                  ; Acc = Address of the file, inside the actual directory
                [32]  JZ .fileNotFound
                    
                [33]  INC #0x0                      ; Skips the first header information, the parent Address
                
                        ; Compares the extension
                [34]  STO &currentAddressHeader     ; startingAddressCurrentFile
                [35]  ACC 0x000000                  ; Get the content of given Address (Acc <- First content of file = Address of the file of the current root directory data)
                [36]  CMP desiredExtension          ; Compare with the target filename extension    
                [37]  JNE .loopNextFile
                
                        ; Compares the filename length
                [38]  INC &currentAddressHeader     ; Increase the Address for the next header information, the filename length
                [39]  ACC currentAddressHeader      ; Load this Address to Accumulator.
                [3A]  ACC 0x000000                  ; Load the content of this Address
                [3B]  CMP desiredFilenameLength     ; Compare with the target filename length
                [3C]  JNE .loopNextFile
                
                [3D]  STO &counterFilenameLength    ; Stores the counter of the filename length for the next step
                
                [3E]  ACC desiredFilenameFirstAddress        ; desiredFilenameFirstAddress
                [3F]  STO &currentAddressDesiredFilename     ; currentAddressDesiredFilename
                    
                    ; Compares the filename
.compFilenameLoop:      
                [40]  ACC currentAddressDesiredFilename     ; Load the desired Filename Address
                [41]  ACC 0x000000                          ; Load the content of this Address
                [42]  STO &currentContentDesiredFilename    ; Store the content
                
                [43]  INC &currentAddressHeader             ; Increase the Address for the next header information, the filename (and keeps increasing for the subsequent words)
                [44]  ACC currentAddressHeader              ; Load to the Accumulator the Address of header
                [45]  ACC 0x000000                          ; Load to the Accumulator the content of its Address
                
                [46]  CMP currentContentDesiredFilename     ; Compare with the header 
                [47]  JNE .loopNextFile                     ; If name doesn't match, loop for another file.
                
                [48]  DEC &counterFilenameLength    ; Decreases the counter
                [49]  ACC counterFilenameLength     ; Load it to the Accumulator
                [4A]  JNZ .compFilenameLoop         ; Loop again if counter isn't zero
                [4B]  ACC currentFileAddress        ; File found, return its Address      
                [4C]  RET 0x000000
                
.fileNotFound:
                [4D]  ACC #0x000000
                [4E]  RET 0x000000
                
; _print(lengthWords, startingAddress)
;
;
_local:
    lengthInWords: {}
    currentAddress: {}
    maxAddress: {}
    
_printText:     [4F]  POP &currentAddress   ; Pop Current Address
                [50]  POP &lengthInWords    ; Pop Length Words
                [51]  ACC currentAddress
                [52]  ADD lengthInWords
                [53]  STO &maxAddress
                [54]  ACC currentAddress    ; Load the current Address
.loop:          [55]  ACC 0x000000          ; Load the content of this Address
                [56]  TXT 0x000000          ; Prints the current content
                [57]  INC &currentAddress   ; Increases the current Address
                [58]  ACC currentAddress
                [59]  CMP maxAddress
                [5A]  JL .loop
                [5B]  RET 0x000000
                
_rootDirNotFound:
                [5C]  PSH .return1      ; Push return location
                [5D]  PSH ER100_STR_LEN ; Push words amount
                [5E]  PSH &ER100_STR    ; Push starting Address
                [5F]  JMP _printText
.return1:       [60]  JMP _deadEnd


_bootDirNotFound:
                [61]  PSH .return1      ; Push return location
                [62]  PSH ER101_STR_LEN ; Push words amount
                [63]  PSH &ER101_STR    ; Push starting Address
                [64]  JMP _printText
.return1:       [65]  JMP _deadEnd


_bootExeNotFound:
                [66]  PSH .return1      ; Push return location
                [67]  PSH ER102_STR_LEN ; Push words amount
                [68]  PSH &ER102_STR    ; Push starting Address
                [69]  JMP _printText
.return1:       [6A]  JMP _deadEnd
                
_deadEnd:       [6B]  NOP #0x000000     ; Keeps repeating itself for ever, until system shutdown.
                [6C]  JMP _deadEnd

_foundBootExe:
                [6D]  JMP 0x000000
